apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
    test {
        java {
            srcDirs = ["test/unit"]
        }
    }
    integrationTest {
        java {
            srcDirs = ["test/integration"]
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDirs = ["resources/test"]
        }
    }
}

jar {
    manifest {
        attributes "Main-Class": "bernabito.dnsproxyfilter.cli.Main"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    testCompile "junit:junit:4.12",
            "org.mockito:mockito-core:2.8.47",
            "org.powermock:powermock-core:1.7.0",
            "org.powermock:powermock-api-mockito2:1.7.0",
            "org.powermock:powermock-module-junit4:1.7.0"
    integrationTestCompile "junit:junit:4.12"
    compile "com.beust:jcommander:1.72"
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

task wrapper(type: Wrapper) {
    gradleVersion = "4.0"
}